table _Severity
	lineageTag: 1a92feb6-85fb-43a8-88bb-9033582841e1

	measure 'Severity Encounter %' = ```
			
			VAR Num =
			    SUM ( vw_Fact_KPI_SeverityMix_BySeverity[Encounter_Count] )
			VAR Den =
			    CALCULATE (
			        SUM ( vw_Fact_KPI_SeverityMix_BySeverity[Encounter_Count] ),
			        REMOVEFILTERS ( Dim_ClinicalClass )
			    )
			RETURN
			    DIVIDE ( Num, Den )
			
			```
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: 0d681068-6f35-4ab5-9770-fa46e04f9580

	measure 'Extreme Severity %' = ```
			
			DIVIDE(
			    [Extreme Encounters],
			    CALCULATE( [Total Encounters], REMOVEFILTERS( Dim_ClinicalClass ) )
			)
			
			```
		lineageTag: dbaec5fa-2515-403e-9623-85858aa1e8eb

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Peer Group Median Extreme %' = ```
			
			VAR CurrentPG = SELECTEDVALUE( Dim_PeerGroup[PeerGroup_Name] )
			RETURN
			CALCULATE(
			    MEDIANX(
			        VALUES( Dim_Facility[Facility_Key] ),
			        [Extreme Severity %]
			    ),
			    REMOVEFILTERS( Dim_ClinicalClass ),
			    ALL( Dim_Facility ),
			    Dim_PeerGroup[PeerGroup_Name] = CurrentPG
			)
			
			
			```
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: d3fa44b6-8a8b-4836-bcce-a65f036d0628

	measure 'Extreme % vs Peer Median' = ```
			
			[Extreme Severity %] - [Peer Group Median Extreme %]
			
			
			```
		formatString: 0.0%;-0.0%;0.0%
		lineageTag: 80582048-dbc5-43b6-8fd4-bdc28ed246e0

	measure 'Extreme Above Peer Median Flag' = ```
			
			IF(
			    [Extreme % vs Peer Median] > 0,
			    "Above Peer Median",
			    "At or Below Peer Median"
			)
			
			```
		lineageTag: 2bf14f6e-d8c2-4bcf-81f3-c1b1b8ce6320

	measure 'Total Encounters' = ```
			
			SUM ( vw_Fact_KPI_SeverityMix_BySeverity[Encounter_Count] )
			
			```
		formatString: 0
		lineageTag: 660a6176-8cc0-4625-b07c-f87f826105ca

	measure 'Extreme Encounters' = ```
			
			CALCULATE(
			    [Total Encounters],
			    REMOVEFILTERS( Dim_ClinicalClass ),
			    Dim_ClinicalClass[APR_Severity_Description] = "Extreme"
			)
			
			```
		formatString: 0
		lineageTag: 485287fa-08f6-403b-bd67-d60b0c325242

	measure 'Median Extreme % (Selected)' = ```
			
			VAR SelectedFacilities =
			    CALCULATETABLE(
			        VALUES( Dim_Facility[Facility_Key] ),
			        -- Apply selected peer groups to the bridge
			        TREATAS(
			            VALUES( Dim_PeerGroup[PeerGroup_Key] ),
			            Bridge_Facility_PeerGroup[PeerGroup_Key]
			        ),
			        -- Then propagate to facilities
			        TREATAS(
			            VALUES( Bridge_Facility_PeerGroup[Facility_Key] ),
			            Dim_Facility[Facility_Key]
			        )
			    )
			RETURN
			CALCULATE(
			    MEDIANX( SelectedFacilities, [Extreme Severity %] ),
			    REMOVEFILTERS( Dim_ClinicalClass )
			)
			
			```
		formatString: 0.0%;-0.0%;0.0%
		lineageTag: 64aead1c-c363-4697-9c75-c3866cd56851

	column Column
		dataType: int64
		formatString: 0
		lineageTag: 014dd9ee-aa0a-40e8-b20e-cf41115403d3
		summarizeBy: sum
		isNameInferred
		isDataTypeInferred
		sourceColumn: [Column]

		annotation SummarizationSetBy = Automatic

	partition _Severity = calculated
		mode: import
		source = Row("Column", BLANK())

	annotation PBI_Id = 3ab72c50885d4fbd9477cb305117e9d6

	annotation 436ba87b-9c83-4389-a31b-ebd06a36be98 = {"Expression":""}

