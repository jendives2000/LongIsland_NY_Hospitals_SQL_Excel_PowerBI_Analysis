table _Severity
	lineageTag: 1a92feb6-85fb-43a8-88bb-9033582841e1

	measure 'Severity Encounter %' = ```
			
			VAR Num =
			    SUM ( vw_Fact_KPI_SeverityMix_BySeverity[Encounter_Count] )
			VAR Den =
			    CALCULATE (
			        SUM ( vw_Fact_KPI_SeverityMix_BySeverity[Encounter_Count] ),
			        REMOVEFILTERS ( Dim_ClinicalClass )
			    )
			RETURN
			    DIVIDE ( Num, Den )
			
			```
		formatString: 0.00%;-0.00%;0.00%
		lineageTag: 0d681068-6f35-4ab5-9770-fa46e04f9580

	measure 'Extreme Severity %' = ```
			
			DIVIDE(
			    [Extreme Encounters],
			    CALCULATE( [Total Encounters], REMOVEFILTERS( Dim_ClinicalClass ) )
			)
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Extreme Level
		lineageTag: dbaec5fa-2515-403e-9623-85858aa1e8eb

	measure 'Peer Group Median Extreme %' = ```
			
			VAR CurrentPG = SELECTEDVALUE( Dim_PeerGroup[PeerGroup_Name] )
			RETURN
			CALCULATE(
			    MEDIANX(
			        VALUES( Dim_Facility[Facility_Key] ),
			        [Extreme Severity %]
			    ),
			    REMOVEFILTERS( Dim_ClinicalClass ),
			    ALL( Dim_Facility ),
			    Dim_PeerGroup[PeerGroup_Name] = CurrentPG
			)
			
			
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Extreme Level
		lineageTag: d3fa44b6-8a8b-4836-bcce-a65f036d0628

	measure 'Extreme % vs Peer Median' = ```
			
			[Extreme Severity %] - [Peer Group Median Extreme %]
			
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Extreme Level
		lineageTag: 80582048-dbc5-43b6-8fd4-bdc28ed246e0

	measure 'Extreme Above Peer Median Flag' = ```
			
			IF(
			    [Extreme % vs Peer Median] > 0,
			    "Above Peer Median",
			    "At or Below Peer Median"
			)
			
			```
		displayFolder: Extreme Level
		lineageTag: 2bf14f6e-d8c2-4bcf-81f3-c1b1b8ce6320

	measure 'Total Encounters' = ```
			
			SUM ( vw_Fact_KPI_SeverityMix_BySeverity[Encounter_Count] )
			
			```
		formatString: 0
		lineageTag: 660a6176-8cc0-4625-b07c-f87f826105ca

	measure 'Extreme Encounters' = ```
			
			CALCULATE(
			    [Total Encounters],
			    REMOVEFILTERS( Dim_ClinicalClass ),
			    Dim_ClinicalClass[APR_Severity_Description] = "Extreme"
			)
			
			```
		formatString: 0
		displayFolder: Extreme Level
		lineageTag: 485287fa-08f6-403b-bd67-d60b0c325242

	measure 'Median Extreme % (Selected)' = ```
			
			CALCULATE(
			    MEDIANX(
			        ALL( Dim_Facility[Facility_Key] ),
			        [Extreme Severity %]
			    ),
			    REMOVEFILTERS( Dim_ClinicalClass ),
			    REMOVEFILTERS( Dim_Facility )
			)
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Extreme Level
		lineageTag: 64aead1c-c363-4697-9c75-c3866cd56851

	measure 'Extreme vs Median Bar Color' = ```
			
			IF(
			    [Extreme Severity %] > [Median Extreme % (Selected)],
			    "#C00000",   -- red
			    "#6E6E6E"    -- neutral gray
			)
			
			```
		displayFolder: Helper
		lineageTag: 6a350604-ebc6-4f66-941d-ceb6b53edad0

	measure 'Is Above Median Extreme' = ```
			
			IF( [Extreme Severity %] > [Median Extreme % (Selected)], 1, 0 )
			
			```
		formatString: 0
		displayFolder: Helper
		lineageTag: 2a7222a2-67a0-4e23-8b46-106a2716073e

	measure 'Facilities Above Median Extreme' = ```
			
			SUMX(
			    VALUES( Dim_Facility[Facility_Key] ),
			    [Is Above Median Extreme]
			)
			
			```
		formatString: 0
		displayFolder: Helper
		lineageTag: 2f7a2178-e0e7-489c-99e9-5ab33dda803d

	measure 'Facilities Selected' = ```
			
			COUNTROWS( VALUES( Dim_Facility[Facility_Key] ) )
			
			```
		formatString: 0
		displayFolder: Helper
		lineageTag: a70c49dd-9f5f-406e-83b6-5485b95badd4

	measure 'Top Facility Extreme (Name)' = ```
			
			VAR T =
			    TOPN(
			        1,
			        VALUES( Dim_Facility[Facility_Name] ),
			        [Extreme Severity %],
			        DESC
			    )
			RETURN
			MAXX( T, Dim_Facility[Facility_Name] )
			
			```
		displayFolder: Helper
		lineageTag: 53d1a359-32b9-4db9-be91-cc21a257f561

	measure 'Top Facility Extreme (%)' = ```
			
			VAR T =
			    TOPN(
			        1,
			        VALUES( Dim_Facility[Facility_Name] ),
			        [Extreme Severity %],
			        DESC
			    )
			RETURN
			MAXX( T, [Extreme Severity %] )
			
			```
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Helper
		lineageTag: f15d59d1-e9d8-4767-8473-e0bff83c176d

	measure 'SMI Min' = ```
			
			MINX( VALUES( Dim_Facility[Facility_Key] ), [Severity Mix Index] )
			
			```
		displayFolder: Helper
		lineageTag: af9c713a-52d1-4d45-9d7b-ffbb90bf6e08

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'SMI Max' = ```
			
			MAXX( VALUES( Dim_Facility[Facility_Key] ), [Severity Mix Index] )
			
			```
		displayFolder: Helper
		lineageTag: 2ae0d58d-45ac-4f9b-ab09-4c3ca00b6016

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure PeerGroups_Selected = ```
			
			    CONCATENATEX(
			        VALUES( Dim_PeerGroup[PeerGroup_Name] ),
			        Dim_PeerGroup[PeerGroup_Name],
			        ", "
			    )
			```
		displayFolder: Helper
		lineageTag: f8deb001-8bb8-42b9-8aca-289613ec05c2

	measure 'Facility Count' = ```
			
			COUNTROWS (
			    VALUES ( Dim_Facility[Facility_Key] )
			)
			
			```
		formatString: 0
		displayFolder: Helper
		lineageTag: 16ed3103-2ae5-4f16-91b7-e9323195d7b7

	measure 'SMI Peer Median' = ```
			
			MEDIANX (
			    VALUES ( Dim_Facility[Facility_Key] ),
			    [Severity Mix Index]
			)
			
			```
		displayFolder: Helper
		lineageTag: 67bb807e-34b7-4d54-8aea-dfbdfa50a6da

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Facilities Above SMI Median' = ```
			
			VAR PeerMedian = [SMI Peer Median]
			RETURN
			COUNTROWS (
			    FILTER (
			        VALUES ( Dim_Facility[Facility_Key] ),
			        [Severity Mix Index] > PeerMedian
			    )
			)
			
			```
		formatString: 0
		displayFolder: Helper
		lineageTag: 136cf811-d3c1-4c71-a585-9abc4745addc

	measure 'Facilities At or Below SMI Median' = ```
			
			VAR PeerMedian = [SMI Peer Median]
			RETURN
			COUNTROWS (
			    FILTER (
			        VALUES ( Dim_Facility[Facility_Key] ),
			        [Severity Mix Index] <= PeerMedian
			    )
			)
			
			```
		formatString: 0
		displayFolder: Helper
		lineageTag: 95a3bf77-ad7b-41ce-bd60-46e57bed465d

	measure 'Is Above PG Severity Median' = ```
			
			VAR smi = [Severity Mix Index]
			VAR med = [PeerGroup Severity Median (FacilitySafe)]
			RETURN IF(NOT ISBLANK(smi) && smi > med, 1, 0)
			
			
			```
		formatString: 0
		displayFolder: Helper
		lineageTag: bb74ddad-1a4f-465d-b8e7-8068d57df297

	column Column
		dataType: int64
		formatString: 0
		lineageTag: 014dd9ee-aa0a-40e8-b20e-cf41115403d3
		summarizeBy: sum
		isNameInferred
		isDataTypeInferred
		sourceColumn: [Column]

		annotation SummarizationSetBy = Automatic

	partition _Severity = calculated
		mode: import
		source = Row("Column", BLANK())

	annotation PBI_Id = 3ab72c50885d4fbd9477cb305117e9d6

	annotation 436ba87b-9c83-4389-a31b-ebd06a36be98 = {"Expression":""}

